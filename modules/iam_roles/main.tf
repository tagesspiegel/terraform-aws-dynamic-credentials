resource "aws_iam_role" "this" {
  name = var.aws_iam_role_name_override
  assume_role_policy = templatefile("${path.module}/files/aws_iam_role_template.json", {
    statements            = var.statements
    audience              = one(var.tfc_oidc_provider_client_id_list)
    tfc_oidc_provider_arn = var.tfc_oidc_provider_arn
    tfc_hostname          = var.tfc_hostname
  })
}

resource "aws_iam_policy" "this" {
  name        = var.aws_iam_role_name_override
  description = "Grants terraform cloud access to AWS (generated by terraform). This policy is attached to the role created by terraform. Do not modify this policy manually. If you need to modify the permissions, modify the terraform code and re-run terraform."
  policy = jsonencode({
    Version   = "2012-10-17"
    Statement = var.aws_iam_custom_policies
  })
}

resource "aws_iam_role_policy_attachment" "this" {
  role       = aws_iam_role.this.name
  policy_arn = aws_iam_policy.this.arn
}
